||| Several tactics for derivation of particular generators for a constructor regarding to how they use externals
module Test.DepTyCheck.Gen.Auto.Core.ConsDerive

import public Control.Monad.State

import public Test.DepTyCheck.Gen.Auto.Derive

%default total

-------------------------------------------------
--- Derivation of a generator for constructor ---
-------------------------------------------------

--- Interface ---

public export
interface ConstructorDerivator where
  consGenExpr : CanonicGen m => GenSignature -> (con : Con) -> (given : SortedSet $ Fin con.args.length) -> (fuel : TTImp) -> m TTImp

--- Particular tactics ---

||| "Non-obligatory" means that some present external generator of some type
||| may be ignored even if its type is really used in a generated data constructor.
namespace NonObligatoryExts

  ||| Least-effort non-obligatory tactic is one which *does not use externals* during taking a decision on the order.
  ||| It uses externals if decided order happens to be given by an external generator, but is not obliged to use any.
  ||| It is seemingly most simple to implement, maybe the fastest and
  ||| fits well when external generators are provided for non-dependent types.
  export
  [LeastEffort] ConstructorDerivator where
    consGenExpr sig con givs fuel = do

      -- Get dependencies of constructor's arguments
      deps <- downmap ((`difference` givs) . mapIn weakenToSuper) <$> argDeps con.args

      -- Set the goal ;-)
      let argsToBeGenerated = fromFoldable (allFins' _) `difference` givs

      -- Arguments that no other argument depends on
      let kingArgs = argsToBeGenerated `difference` concat deps

      -- Acquire order(s) in what we will generate arguments
      -- TODO to permute independent groups of arguments independently
      let allKingsOrders = allPermutations kingArgs

      -- Derive constructor calling expression for given order of generation
      let genForKingsOrder : List (Fin con.args.length) -> m TTImp
          genForKingsOrder = map (`apply` callCons) . evalStateT argsToBeGenerated . foldlM genForOneKing id where

            bindNameStr : Name -> m String
            bindNameStr $ UN $ Basic n = pure n
            bindNameStr n = fail "Unsupported name \{show n} for the basement of a bind name"

            -- ... state is the set of arguments that are left to be generated
            genForOneKing : (TTImp -> TTImp) -> (king : Fin con.args.length) -> StateT (SortedSet $ Fin con.args.length) m $ TTImp -> TTImp
            genForOneKing leftExprF kingArg = do

              -- Determine which arguments will be on the left of dpair in subgen call
              let depArgs = index kingArg deps `intersection` !get

              -- Make sure generated arguments will be not generated again
              modify $ union depArgs

              -- Form a task for subgen
              let subsig : GenSignature := ?subgen_sig

              -- Form an expression to call the subgen
              subgenCall <- lift $ callGen subsig fuel ?genForOneKing_rhs

              -- Form an expression of binding the result of subgen
              bindKingArg <- map bindVar $ lift $ bindNameStr $ argName $ index' con.args kingArg
              let bindSubgenResult = buildDPair bindKingArg ?bind_king_deps

              -- Chain the subgen call with a given continuation
              pure $ \cont => `(~(subgenCall) >>= \ ~(bindSubgenResult) => ~(cont))

            callCons : TTImp
            callCons = ?callCons_rhs -- actually, needs RHS expression for GADT indices as input

      map callOneOf $ traverse genForKingsOrder $ forget allKingsOrders

  ||| Best effort non-obligatory tactic tries to use as much external generators as possible
  ||| but discards some there is a conflict between them.
  ||| All possible non-conflicting layouts may be produced in the generated values list.
  |||
  ||| E.g. when we have external generators ``(a : _) -> (b : T ** C a b)`` and ``(b : T ** D b)`` and
  ||| a constructor of form ``C a b -> D b -> ...``, we can use values from both pairs
  ||| ``(a : _) -> (b : T ** C a b)`` with ``(b : T) -> D b`` and
  ||| ``(a : _) -> (b : T) -> C a b`` with ``(b : T ** D b)``,
  ||| i.e. to use both of external generators to form the generated values list
  ||| but not obligatorily all the external generators at the same time.
  export
  [BestEffort] ConstructorDerivator where
    consGenExpr sig con givs fuel = do
      ?cons_body_besteff_nonoblig_rhs

||| "Obligatory" means that is some external generator is present and a constructor has
||| an argument of a type which is generated by this external generator, it must be used
||| in the constuctor's generator.
|||
||| Dependent types are important here, i.e. generator ``(a : _) -> (b ** C a b)``
||| is considered to be a generator for the type ``C``.
||| The problem with obligatory generators is that some external generators may be incompatible.
|||
|||   E.g. once we have ``(a : _) -> (b ** C a b)`` and ``(a ** b ** C a b)`` at the same time,
|||   once ``C`` is used in the same constructor, we cannot guarantee that we will use both external generators.
|||
|||   The same problem is present once we have external generators for ``(a : _) -> (b : T ** C a b)`` and ``(b : T ** D b)`` at the same time,
|||   and both ``C`` and ``D`` are used in the same constructor with the same parameter of type ``T``,
|||   i.e. when constructor have something like ``C a b -> D b -> ...``.
|||
|||   Notice, that this problem does not arise in constructors of type ``C a b1 -> D b2 -> ...``
|||
||| In this case, we cannot decide in general which value of type ``T`` to be used for generation is we have to use both generators.
||| We can either fail to generate a value for such constructor (``FailFast`` tactic),
||| or alternatively we can try to match all the generated values of type ``T`` from both generators
||| using ``DecEq`` and leave only intersection (``DecEqConflicts`` tactic).
namespace ObligatoryExts

  export
  [FailFast] ConstructorDerivator where
    consGenExpr sig con givs fuel = do
      ?cons_body_obl_ff_rhs

  export
  [DecEqConflicts] ConstructorDerivator where
    consGenExpr sig con givs fuel = do
      ?cons_body_obl_deceq_rhs
